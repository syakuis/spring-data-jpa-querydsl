// https://docs.spring.io/spring-boot/docs/2.4.5/reference/html/dependency-versions.html#dependency-versions
buildscript {
    ext.groupId = "io.github.syaku"
    ext.versionName = "1.0.0-SNAPSHOT"
    ext.springBootVersion = "2.4.5"
    ext.junitVersion = "5.7.2"
    ext.lombokVersion = "1.18.20"
    ext.h2Version = "1.4.200"
    ext.querydslVersion = "4.4.0"

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }

    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7.1"
        classpath "io.franzbecker:gradle-lombok:3.1.0"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"
    }
}

wrapper {
    gradleVersion = "6.5"
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "io.franzbecker.gradle-lombok"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"

idea {
    module {
        inheritOutputDirs = true
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

repositories {
    mavenCentral()
}

group groupId
version versionName

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

compileJava.options.encoding = 'UTF-8'

def querydslDir = "${buildDir}/generated/querydsl"

sourceSets {
    main.java.srcDir querydslDir
}

configurations {
    testImplementation.exclude group: "junit", module: "junit"
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "org.springframework.boot:spring-boot-starter-data-jpa"

    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-json"
    testImplementation "org.springframework.boot:spring-boot-starter-test"

    testImplementation "com.h2database:h2:${h2Version}"
    compileOnly "com.h2database:h2:${h2Version}"

    implementation "com.querydsl:querydsl-jpa:${querydslVersion}"
    annotationProcessor(
        "jakarta.persistence:jakarta.persistence-api",
        "jakarta.annotation:jakarta.annotation-api",
        "com.querydsl:querydsl-apt:${querydslVersion}:jpa"
    )
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    systemProperty 'spring.profiles.active', 'test'
}

